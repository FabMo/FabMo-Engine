'!FABMO!name:Find Table Origin
'!FABMO!description:Search for Z,Y,X Prox Switches
' ATC HOMING MACRO
&XBackoff = 0.375
&YBackoff = 0.5
&ZBackoff = 0.100
&AxisInput := 1
&DriverInput := 1
&XSearch = 110
&YSearch = 55

Main:
GOSUB CheckInput
GOSUB HomeZ
GOSUB HomeY
GOSUB HomeX

END

HomeX:
	&xs = %(1)
	PX,%(1)-&XSearch,1,2
    &xe = %(1)
    IF (&xs-&xe) >= (&XSearch-0.1) THEN GOTO CouldntFindXProx
    JX,%(1)+&XBackoff
	VA,0,,,,,,0,,,,,
	RETURN
    
HomeY:
	&ys = %(2)
	PY,%(2)-&YSearch,1,3
    &ye = %(2)
    IF (&ys-&ye) >= (&YSearch-0.1) THEN GOTO CouldntFindYProx    
    JY,%(2)+&YBackoff
	VA,,0,,,,,,0,,,,
	RETURN
    
HomeZ:

	PZ,%(3)+10,1,5
    JZ,%(3)-&ZBackoff
	VA,,,,,,,,,0,,,
	RETURN
    
 CheckInput:
 	IF %(52) == 1 THEN GOTO check_input_fail_Prox
    IF %(53) == 1 THEN GOTO check_input_fail_Prox
    IF %(54) == 1 THEN GOTO check_input_fail_driver
    RETURN
    
 check_input_fail_Prox:
    'Tool is on the Prox switch.  
    'pause
    FAIL "Input switch is already tripped.  Cannot zero the tool."
    
 check_input_fail_driver:
    'Driver Fault. Reset input 4 then click resume or quit to exit.
    pause
    goto Main
    End 
    
CouldntFindXProx:
	FAIL "Could not find the X prox switch.  Check input switch connections and try again."

CouldntFindYProx:
	FAIL "Could not find the Y prox switch.  Check input switch connections and try again."
