'!FABMO!name:Home Tool
'!FABMO!description: Automatically Set Axis Zero for XYZ and position tool
'Home XYZ
$Run_Auto = 0
SO,5,0
SC,0
&Zoffset = 0

&Atarget = 0
&Atemp = 0	
VA,,,&Zoffset,0,0,,,,0,0,0,
if $Toolin = 0 then gosub Set0
if $Toolin = 1 then gosub Set1
if $Toolin = 2 then gosub Set2
if $Toolin = 3 then gosub Set3
if $Toolin = 4 then gosub Set4
if $Toolin = 5 then gosub Set5
IF %(56) = 1 THEN GOSUB GET_OFF_Z_LIMIT
IF %(55) = 1 THEN GOSUB GET_OFF_A_LIMIT
IF %(52) = 1 THEN GOSUB GET_OFF_X_LIMIT
CHECKY:
IF %(53) = 1 THEN GOTO GET_OFF_Y_LIMIT

gosub FindZ
goto FindAFast
END

FindZ:

	PZ, 15, 3, 6
	VA,,,&Zoffset + $PlateOffset,,,,,,&Zoffset + $PlateOffset,,,
	MZ,&Zoffset + $PlateOffset -.25
	PZ, 15, 0.5, 6
	VA,,,&Zoffset + $PlateOffset,,,,,,&Zoffset + $PlateOffset,,,
	MZ,&Zoffset + $PlateOffset -.25
return
	
FindAFast:
	&Atarget = %(55)
    &Atemp = %(4)
    if &Atarget = 1 then goto setAFast
    MA, %(4) + 0.25
    IF %(4) > 10 THEN GOTO A_PROBLEM
    GOTO FindAFast
    
setAFast:
	VA,,,,0,,,,,,0,,
    MA, -.25
    goto FindASlow
    
FindASlow:
	&Atarget = %(55)
    &Atemp = %(4)
    if &Atarget = 1 then goto setASlow
    MA, %(4) + 0.03
    GOTO FindASlow

setASlow:
	VA,,,,0,,,,,,0,,
    MA, -.015
    goto FindAFinal

FindAFinal:
	&Atarget = %(55)
    &Atemp = %(4)
    if &Atarget = 1 then goto setAFinal
    MA, %(4) + 0.0005
    GOTO FindAFinal
    
setAFinal:
    PAUSE 1
    VA,,,,($Aoffset)/25.4,,,,,,($Aoffset)/25.4,,
    PAUSE 1
    MA, %(4) - 0.25
    goto FindY

FindY:
	PY, -60, 5, 3
	VA,,-.5,,,,,,-.50,,,
    MY,-0.25
    PY, -60, 0.5, 3
    VA,,-.5,,,,,,-.50,,,
    MY,0
	goto FindX

FindX:
	PX, -200, 5, 2
	VA,-.5,,,,,,-.50,,,
	MX,-0.25
	PX, -200, 0.5, 2
	VA,-.5,,,,,,-.50,,,
	MX,0
    
ENDIT:
END
GOTO ENDIT

Set0:
	&Zoffset = 0
return
Set1:
	&Zoffset = $TOOL1L
return
Set2:
	&Zoffset = $TOOL2L
return
Set3:
	&Zoffset = $TOOL3L
return
Set4:
	&Zoffset = $TOOL4L
return
Set5:
	&Zoffset = $TOOL5L
return

GET_OFF_Z_LIMIT:
MZ,%(3)-2
RETURN

GET_OFF_A_LIMIT:
MA,%(4)-2
RETURN

GET_OFF_Y_LIMIT:
IF %(2) > 45 THEN GOTO GET_OFF_Y_OTHER_WAY
MY,%(2)+1
IF %(53) = 1 THEN GOTO GET_OFF_Y_OTHER_WAY
GOTO CHECKY

GET_OFF_X_LIMIT:
MX,%(1)+2
RETURN
    
GET_OFF_Y_OTHER_WAY:
MY,%(2)-2
GOTO CHECKY

A_PROBLEM:
PAUSE "Error, Restart Homing Routine"
END