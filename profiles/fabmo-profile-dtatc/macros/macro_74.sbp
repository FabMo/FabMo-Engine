'!FABMO!name:ATC Calibrate
'!FABMO!description:Initial calibration of Tool-Clip location for ATC
'!FABMO!enabled:true
' ATC Calibration; records location of all the tool clips.
' Brian Owen
' Refactored 2/09/2026 (th)
' ## Note this Macro still uses a combination of local and persistent variables; we should clean this up at some point but it is functional for now.

' Main =========================================================================
$ATC.type = 7                                                                   ' Set to ATC

CN,3                                                                            ' Home machine in XYZ first. 
VN,0                                                                            ' ## functional??? Turn off limit switches
SF,0                                                                            ' ## functional??? Turn off table limits

&u = %(25)
&debug = 0 
JS,$xyJogUU[&u],$zJogUU[&u]                                                     ' Set Jog speeds

PAUSE "How many positions exist on your toolbar" &count                         ' Maybe user does not want to use all 7 positions...so give them a choice
$ATC.numClips = &count                                                          ' Update ATC settings based on user selection
J2,$firstClipUU[&u].x - %(7),$firstClipUU[&u].y - %(8)                          ' Jog to known first clip location. Offset by table base coordinates
PAUSE "Insert an empty tool holder in the spindle (no collet or nut)... (M-0051)"
PAUSE "...Insert an empty tool holder upside down in the #1 clip (no collet or nut)... (M-0052)"
PAUSE "Place one end of the calibration clip on the pull stud on tool holder #1 and the other end on the fixed z zero plate. Click OK when ready to proceed! (M-0055)"

'Check Z Zero Input
    IF %($sb_zZeroCk) == 1 THEN GOSUB CONTACT_TRIGGERED_ERROR                    ' We're about to lower the empty tool holder to touch the inverted tool holder in the clip. If input 1 is already triggered then we have a problem
    &check = %(3) - %(106)                                                       ' Calculate max Z travel for probe move
    PZ, &check,  $xySlowUU[&u], $sb_zZeroNum                                     ' Try to touch tool holders together
    IF &Debug = 1 THEN PAUSE "Press OK"
    'IF %(3) <= &check THEN MZ,%(3) + $smallBackOffUU[&u]                        ' If we didn't make contact -- let's back off just in case the problem was a lack of electical continuity and we've crashed
    IF %(3) <= &check THEN GOSUB CONTACT_ERROR

'Set Clip Height
    &Z_clipbase = %(3) - $ATC_holderHeightUU[&u]                                 ' If we succeeded in making contact, we record the current position for use later
    JZ, %(3) + $smallBackOffUU[&u]                                               ' Back off to break contact
    JY,%(2) - $ToolBar_Probe_OffsetUU[&u]                                        ' Shift over in the Y to prepare for first Y probe of calibration tool holder
    &start_X = %(1)                                                              ' Record X start position
    &start_Y = %(2)                                                              ' Record Y start position
    &start_Z = %(3)                                                              ' Record Z start position
    &alignment_height = %(3) - $ATC_holderAlignmentHeightUU[&u] - $smallBackOffUU[&u]    ' Calculate correct z position for plunge
    JZ,&alignment_height                                                         ' Move down to prepare for Y probe
    &check = $firstClipUU[&u].y - %(8)                                           ' Calculate max distance for Y probe
    PY, $firstClipUU[&u].y - %(8), $xySlowUU[&u], $sb_zZeroNum                   ' Probe in Y to find inverted tool holder
    IF &Debug = 1 THEN PAUSE "Press OK"
    'IF %(2) >= &check THEN MY,%(2) - $smallBackOffUU[&u]                        ' If we went too far without contact -- first back off in case we crashed
    IF %(2) >= &check THEN GOSUB CONTACT_ERROR

'Found Left Edge
    PAUSE 0.1                                                                    ' Take a break
    &LEFTSTART = %(2)                                                            ' Record Y position for use in calculating center location of tool holder
    JY, &LEFTSTART - $smallBackOffUU[&u]                                         ' Break contact between tool holders
    JZ, %(3) + $safe_liftUU[&u]                                                  ' Lift up clear of the tool holder
    JY, &LEFTSTART + $TOOL_OFFSETUU[&u]                                          ' Move to other side of tool holder
    JZ,&alignment_height                                                         ' Drop down to prepare for probe
    PY, &LEFTSTART, $xySlowUU[&u], $sb_zZeroNum                                  ' Probe in Y to find inverted tool holder
    IF &Debug = 1 THEN PAUSE "Press OK"
    'IF %(2) <= &LEFTSTART THEN MY,%(2) + $smallBackOffUU[&u]                    ' If we went too far without contact -- first back off in case we crashed
    IF %(2) <= &LEFTSTART THEN GOSUB CONTACT_ERROR
    
'Found Right Edge
    PAUSE 0.1                                                                    ' Take a break
    &RIGHTSTART = %(2)                                                           ' Record Y position for use in calculating center location of tool holder
    JY,&RIGHTSTART + $smallBackOffUU[&u]                                         ' Break contact between tool holders
    JZ, %(3) + $safe_liftUU[&u]                                                  ' Lift up clear of the tool holder
    &CENTER_Y = &LEFTSTART + ((&RIGHTSTART - &LEFTSTART) / 2)                    ' Calculate center position in Y axis
    J2,$firstClipUU[&u].x - $TOOL_OFFSETUU[&u] - %(7), &CENTER_Y                 ' Jog in front of tool holder
    JZ,&alignment_height                                                         ' Drop down to prepare for probe
    &check = $firstClipUU[&u].x - %(7)                                           ' Calculate max distance for X probe
    PX,$firstClipUU[&u].x - %(7), $xySlowUU[&u], $sb_zZeroNum                    ' Probe in X to find inverted tool holder
    IF &Debug = 1 THEN PAUSE "Press OK"
    'IF %(1) >= &check THEN MX,%(1) - $smallBackOffUU[&u]                        ' If we went too far without contact -- first back off in case we crashed
    IF %(1) >= &check THEN GOSUB CONTACT_ERROR

'Found Front Edge
    PAUSE 0.1                                                                    ' Take a break
    &X_EDGE = %(1)                                                               ' Record X location of tool holder
    JX,&X_EDGE - $smallBackOffUU[&u]                                             ' Break contact with tool holder
    &CENTER_X = &X_EDGE + $holder_radiusUU[&u] * 2                               ' Calculate X location of tool holder center
    JZ,%(3) + $safe_liftUU[&u]                                                   ' Lift up clear of tool holder
    JX,&CENTER_X                                                                 ' Move to center of tool holder to demonstrate that we've calculated the correct position

    &center_X = &center_X - $sb_homeOffUU[&u].x                                  ' Clean offsets for storage
    &center_Y = &center_Y - $sb_homeOffUU[&u].y
    &thisZ = &Z_clipbase + %(9)

    &toolIdx = 1                                                                 ' Set up index that will be incremented while recording values for each tool
    TOOL_POS_LOOP:
          $toolsUU[&toolIdx][].x = &center_X                                   ' Record locations of clips (permanent)
          $toolsUU[&toolIdx][].y = &center_Y + $TOOL_OFFSETUU[&u] * (&toolIdx - 1)    ' (Y location of clip increments based on tool clip offset variable)
          $toolsUU[&toolIdx][].z = &thisZ
          $toolsUU[&toolIdx][].h = -5                                          ' stub in a bit length
        IF &toolIdx = $ATC.numClips THEN GOTO FINISHED                           ' If we've done all the clips we planned to do -- head for the exit
        &toolIdx = &toolIdx + 1
        GOTO TOOL_POS_LOOP                                                       ' Go through loop for next tool

FINISHED:
    $atcUU[].zZero_X = %(1)                                                      ' Store locations of fixed Z Zero plate (always the same)
    $atcUU[].zZero_Y = %(2) + $atcUU[&u].zZero_YCorrection                       ' with OFFSET CORRECTION (typ. -4.125in)
    J2,0,0
    DIALOG "Calibration complete (M-0151)" OKTEXT="Continue ..."
END


' Error Handling ===============================================================
CONTACT_TRIGGERED_ERROR:
    PAUSE "Your Z Zero input is already triggered, if the z zero plate on your gantry is plugged in, please unplug and press OK."
    END
    RETURN

CONTACT_ERROR:
    PAUSE "Unable to make contact with tool holder, check that grounding cable is connecting pull stud to fixed z zero plate."
    END
    RETURN
