'!FABMO!name: Square Tool
'!FABMO!description: Squares Deskop Tool
' Name: Square Tool
' Description: Squares Deskop Tool [C10]
' ***Desktop Only
'   - note that DT and DT Max will be squaring the same gantry but it is on different axes depending on tool
' Author: Ryan Sturmer, Brendan Collins
' History: 2018/04/3 - created
' 2026/2/12 refactored (th)

'--- Adjust Standard Macro Variables to Current Tool Units if Needed ---
'--- Check to make sure Standard Macro Variables have been read
$sb_standard_variables_PRESENT := 0                                             ' Define test variable if it does not exist
IF $sb_standard_variables_PRESENT == 0 THEN GOSUB no_variables
&u = %(25)                                                                      ' Read current tool units for use in adjusting variables if needed
&backOff_dist = $sb_oochUU[&u] * 5

' === Main Program ===
  PAUSE "This routine will Square Gantry by pushing it into End Stops (loudly). Resume to Continue."
  Z3
  GOSUB clear_z
  IF &dtType = 3 then GOTO dt3
      GOSUB square_x_gantry
      GOSUB run_C3
  END
  dt3:
      GOSUB square_y_gantry
      GOSUB run_C3
  END
'====================

'--- 'Subroutines ---
clear_z: 
    PZ, $sb_searchDistUU[&u].z, $sb_searchSpeedUU[&u].z, $sb_proxNum.z          ' Search for prox switch
    &Z_AT = %(3)                                                                ' Get where we're at to backoff from
    JZ, &Z_AT - (4 * &backOff_dist)
    RETURN

square_y_gantry:
    PY, $sb_searchDistUU[&u].y, $sb_searchSpeedUU[&u].y, $sb_proxNum.y           ' Search for prox switch
    VA,,0,,,,,,0,,,,                                                             ' Set 0 location
    VR, $sb_squaringJerkUU[&u]
    MY, $sb_squaringMoveUU[&u]                                                   ' Move Y axis .75 inches past prox switch, which will force it into end stop  
    VA,,0,,,,,,0,,,,                                                             ' Zero Y again so we know where we are at
    VR,150
    MY, ($sb_squaringMoveUU[&u] * -5)
    VA,,18,,,,,,0,,,,
    RETURN

square_x_gantry:
    PX, $sb_searchDistUU[&u].x, $sb_searchSpeedUU[&u].x, $sb_proxNum.x           ' Search for prox switch
    VA,0,,,,,,0,,,,,                                                             ' Set 0 location
    VR, $sb_squaringJerkUU[&u]
    MX, $sb_squaringMoveUU[&u]                                                   ' Move Y axis .75 inches past prox switch, which will force it into end stop  
    VA,0,,,,,,0,,,,,                                                             ' Zero Y again so we know where we are at
    VR,150
    MX, ($sb_squaringMoveUU[&u] * -5)
    VA,36,,,,,,0,,,,,
    RETURN

run_C3: 
    PAUSE "Tool is squared.You should run the Homing Macro now? Press RESUME to Run; QUIT to Skip."
  ' * MAYBE Jog the squared axis back near zero in above routines ???
    C3
    RETURN

no_variables:
    C201
    RETURN
