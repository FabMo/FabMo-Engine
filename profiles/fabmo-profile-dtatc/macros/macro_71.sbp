'!FABMO!name:ATC Toolchange
'!FABMO!description:Macro Description
'!FABMO!enabled:true
$unitMultiplier = 1
IF %(25) = 0 THEN GOTO SETUPVALUES
$unitMultiplier = 25.4
SETUPVALUES:
&loadVariables = 1
IF &loadVariables = 0 THEN GOTO START
JS,8,8
C70
START:
&MANUAL_CHANGE = 0
&ATC_safeclear =  -1 * %(9) - $smallBackOff
JZ,&ATC_safeclear

  'Preliminary Checks..............................................................................................

 'Check ATC STATUS?
    If $ATC.Status = "OK" Then GoTo OK_SWAP_TOOLS
	If $ATC.ToolIn >= 1000 THEN GOTO OK_SWAP_TOOLS
    If $ATC.Status = "EMPTY" Then GoTo settool0
    PAUSE "Problem with ATC, remove tool and re-home machine."
 END

OK_SWAP_TOOLS:

Position_Z:
    &tx = $tools[$ATC.ToolIn].X - %(7) + $sb_homeOff_X * $unitMultiplier
    &ty = $tools[$ATC.ToolIn].Y - %(8) + $sb_homeOff_Y * $unitMultiplier
    &tz = $tools[$ATC.ToolIn].Z - %(9)
    &th = $tools[$ATC.ToolIn].H
    &sx = $tools[$ATC.ToolIn].X + $ATC_clipSafe - %(7)
	If $ATC.ToolIN >= 1000 Then GoTo setSpecialTool
   	If $ATC.ToolIN < 0 Then &MANUAL_CHANGE = 1
	If $ATC.ToolIN > $ATC.NumClips Then &MANUAL_CHANGE = 1
    If $ATC.ToolIN=0 Then GoTo settool0

Droptool:
    &Z_OFFSET = %(9) - (&th + $ATC.PlateOffset)
	&COUNT = 0
	&BIGCOUNT = 0
    'Check status of toolbar.
    If &tool = $ATC.ToolIN Then GoTo Same_Tool 'NO NEED TO DO TOOL CHANGE
	'IF %($ToolBar_Status) = 0 THEN  &MANUAL_CHANGE = 1
	IF &MANUAL_CHANGE = 1 THEN GOTO MANUAL_CHANGE
	If %(1) < &sx THEN GOTO SKIP_SAFE
    JX,&sx
    SKIP_SAFE:
	  J2, &sx, &ty          'Jog to X zero and Y midpoint between tool holders

Clip:
    JZ, &tz       'Jog to Z height of toolholder clip
	JX, &tx + $ATC_clipSafe / 4
    SO, $DrawBar,1               'Open toolholder chuck
    MX, &tx               'Move into clip
    &Status = %($Drawbar_Sensor_Status)
    If &Status = 0 Then GoTo bardroperror
    JZ, %(3)+ $thClearance * 2          'Move up to clear toolholder
    SO, $DrawBar,0               'Close toolholder chuck
	&ATC_safeclear = -1 * %(9) + $Transit_Height
    JZ,&ATC_safeclear
    If %($Tool_Sensor_Status) = 1 Then GoTo toolreleaseerror

ZeroToolDrop:
	&Dropped = 1
	&MANUAL_CHANGE = 0
    If &tool >= 1000 Then GoTo setSpecialTool
    If &tool=0 Then GoTo settool0
	If &tool > $ATC.numClips Then &MANUAL_CHANGE = 1
    
Changetool:
	&COUNT = 0
	&BIGCOUNT = 0
	'IF %(56) = 0 THEN  &MANUAL_CHANGE = 1
    IF &MANUAL_CHANGE = 0 THEN GOTO SKIP_CLEAR
    &ATC_safeclear = -1 * %(9) - $smallBackOff
    JZ,&ATC_safeclear
    SKIP_CLEAR:
    &th = $tools[&tool].H
  'Setup new Z height of bit relative to table surface from Table Base Coordinates
    &new_curZ = %(3) - ((&th + $ATC.PlateOffset) + &Z_OFFSET - %(9))   'update to current Z
    VA,,,&new_curZ
	IF &MANUAL_CHANGE = 1 THEN GOTO MANUAL_CHANGE
	&tx = $tools[&tool].X - %(7) + $sb_homeOff_X * $unitMultiplier
    &ty = $tools[&tool].Y - %(8) + $sb_homeOff_Y * $unitMultiplier
    &tz = $tools[&tool].Z - %(9)
    &sx = $tools[&tool].X + $ATC_clipSafe - %(7)
  'Skip pick up if zero tool number and leave spindle empty
    If &tool = 0 Then GoTo Finish_ToolChange
    J2, &tx, &ty
  CT1:
    JZ, &tz + $thClearance * 2
    SO, $DrawBar,1                  'Open toolholder chuck
    PZ,&tz + $thClearance,8,$Drawbar_Sensor
	If %($Drawbar_Sensor_Status) == 0 Then GoTo barlifterror
    JZ, &tz       'Move to toolholder height
    SO, $DrawBar,0                  'Close toolholder chuck
    PX, &tx + $ATC_clipSafe / 4,1,1
    &Status = %($Tool_Sensor_Status)
    If %($Tool_Sensor_Status) == 0 Then GoTo barlifterror
	JX, &tx + $ATC_clipSafe
    JX, &sx
    &ATC_safeclear = -1 * %(9) - $smallBackOff
	JZ,&ATC_safeclear
	JX, &sx + $ATC_clipSafe
      $ATC.Dropped = 0
      GoTo Finish_ToolChange

TOOL_NOT_REMOVED:
'Tool still detected in spindle, please try again. (M-0145)
PAUSE
MANUAL_CHANGE:
IF $ATC.Dropped = 1 THEN GOTO INSERT_TOOL
REMOVE_TOOL:
J2,$manual_change_X - %(7),$manual_change_Y - %(8)
PAUSE "Press green button on gantry to remove current tool. (M-0139)"
IF %($Tool_Sensor_Status) = 1 THEN GOTO TOOL_NOT_REMOVED
GOTO ZeroToolDrop
INSERT_TOOL:
J2,$manual_change_X - %(7),$manual_change_Y - %(8)
PAUSE "Press green button on gantry and insert tool"
$ATC.Dropped = 0
GOTO Finish_ToolChange

Finish_ToolChange:
    $ATC.ToolIN = &tool
    IF &tool > 0 THEN GOTO ENDMACRO
Same_Tool:
    &ATC_safeclear = -1 * %(9) - $smallBackOff
    JZ,&ATC_safeclear

ENDMACRO:
	  &TL = &th + $ATC.PlateOffset + $ATC.MaxDepth
	  VL,,,,,&TL
End
'END of ATC Main Routine =================================================================================================

settool0:
	&tx = $tools[0].X - %(7)
    &ty = $tools[0].Y - %(8)
    &tz = $tools[0].Z - %(9)
    &th = $tools[0].H
    &sx = $tools[0].X + $ATC_clipSafe - %(7)
  If &MANUAL_CHANGE = 1 THEN GOTO MANUAL_CHANGE
  If &tool = 0 Then GoTo Changetool
  &Z_OFFSET = %(9) - (&th + $ATC.PlateOffset)
GoTo ZeroToolDrop
 
 setSpecialTool:
  C79 'Special Tool Change?
  If &SpecialError = 1 Then GoTo SpecialError
  If $ATC.ToolIn >= 1000 Then GoTo ZeroToolDrop
  If &tool >= 1000 Then GoTo Finish_ToolChange
  GoTo SpecialError

SpecialError:
  'Special tool not set up
  PAUSE
  End

toolerror:
  PAUSE "Error grabbing tool check tool changer. Any key to exit (M-0043)"
  End

toolreleaseerror:
  PAUSE "Error releasing tool check tool changer. Any key to exit (M-0138)"
  End

barlifterror:
  SO, $DrawBar,0  'Close toolholder chuck, as may be left on/open
  PAUSE 2
  &COUNT = &COUNT + 1
  IF &COUNT < 3 THEN GOTO CT1
  'Toolbar not opening properly, check air pressure (M-0163)
  PAUSE
  GOTO CT1

bardroperror:
  SO, $DrawBar,0  'Close toolholder chuck, as may be left on/open
  PAUSE 2
  &COUNT = &COUNT + 1
  IF &COUNT < 3 THEN GOTO Clip
  &BIGCOUNT = &BIGCOUNT + 1
  'Tool holder not releasing properly, pausing for spindle to cool before attempting toolchange again.
  PAUSE 120
  &COUNT = 0
  IF &BIGCOUNT < 3 THEN GOTO Clip
  'Toolbar not opening properly, check air pressure (M-0163)
  PAUSE
  GOTO Clip

bartoperror:
  SO, $DrawBar,0  'Close toolholder chuck, as may be left on/open
  PAUSE "Drawbar did not OPEN properly. Any key to exit (M-0044)"
  End

barboterror:
  PAUSE "Drawbar did not CLOSE properly. Any key to exit (M-0045)"
  End