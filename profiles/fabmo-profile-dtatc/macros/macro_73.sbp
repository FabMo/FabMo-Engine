'!FABMO!name:ATC Plate Offset
'!FABMO!description:Set distance from ATC Measurement Plate to Material
'!FABMO!enabled:true
' Refactored 2026/02/09 (th)

&u = %(25)
CN,70                                                            ' Load all ATC Settings (and local variables from #201)
GOSUB initialize_local_variables
GOSUB HOME_Z                                                     ' First Home the Z Axis

PAUSE "The keypad will now open. Move your spindle to the location where you want to Zero the Z axis." 
SK                                                               ' Keypad open
CN,2                                                             ' Zero Z Axis to Z Zero Plate
PAUSE "Offset 9 is " + %(9)                                        ' Display offset for user's reference
$atcUU[].PlateOffset = %(9) - $toolsUU[$ATC.ToolIn][&u].h        ' Record Offset in config
PAUSE "tool.h- " + $toolsUU[$ATC.ToolIn][&u].h + " ATCPlateOffset- " + $atcUU[&u].PlateOffset
END

' Subroutines ==========================================================
HOME_Z:
    PZ, &searchDist_Z, &searchSpeed_Z, &proxNum_Z                 ' PZ probes up for top limit
    ZZ                                                            ' Reset working zero
    JZ, (-1 * &homeOff_Z)                                         ' Pull back from prox
    &target_ck = &homeOff_Z + &ooch                               ' New target for detecting error just past prox
    PAUSE .2                                                      ' Time to sense clearing target
    PZ, &target_ck, &slowSearchSpeed_Z, &proxNum_Z                ' Slowly probe for prox in final accurate check
    IF %(3)== &target_ck THEN GOSUB fail_missed_target            ' Look at current Z location to see if we went past the target
    ZZ
    JZ, (-1 * &homeOff_Z)
    VA,,,-1*($toolsUU[$ATC.ToolIn][&u].h + $atcUU[&u].PlateOffset)       ' Apply Offset for no tool
    RETURN

' Error Messages =======================================================
fail_missed_target:
    PAUSE "Target Not Triggered! Lower Z Axis and run C3 (XYZ Homing)."  ' Did not hit target.
    END

initialize_local_variables:
    &proxNum_Z = $sb_proxNum.z
    &check_prox_Z = $sb_proxCk.z							  
    &proxNum_X = $sb_proxNum.x
    &check_prox_X = $sb_proxCk.x							  
    &proxNum_Y = $sb_proxNum.y
    &check_prox_Y = $sb_proxCk.y
    &proxNum_zZero = $sb_zZeroNum
    &check_prox_zZero = $sb_zZeroNum
	&target_ck = 0
	&ooch = $sb_oochUU[&u]
	&backOff_dist = &ooch * 5
	&searchDist_Z = $sb_searchDistUU[&u].z
	&searchSpeed_Z = $sb_searchSpeedUU[&u].z
	&slowSearchSpeed_Z = $sb_slowSearchSpeedUU[&u].z
	&searchDist_X = $sb_searchDistUU[&u].x
	&searchDist_Y = $sb_searchDistUU[&u].y
	&searchSpeed_XY = $sb_searchSpeedUU[&u].xy
	&slowSearchSpeed_XY = $sb_slowSearchSpeedUU[&u].xy
	&homePullBack = $sb_homePullBackUU[&u]
	&homeOff_Z = $sb_homeOffUU[&u].z
	&homeOff_X = $sb_homeOffUU[&u].x
	&homeOff_Y = $sb_homeOffUU[&u].y
    RETURN
end

