'!FABMO!name:*ATC* PLATE OFFSET
'!FABMO!description:Set distance from ATC Measurement Plate to Material or Bed
'!FABMO!enabled:true
' Name: *ATC* PLATE OFFSET
' Description: Set distance from ATC Measurement Plate to Material or Bed
' Author: Brian Owen
' 2026/2/12 - Refactored (th)


&u = %(25)
CN,75                                                                ' Load primary ATC variables and ck status (calls CN 70)

'=== Main ==============================================================
GOSUB HOME_Z                                                         ' First Home the Z Axis
PAUSE "The keypad will now open. Move your spindle to the location where you want to Zero the Z axis." 
SK "Move your spindle to the location for Zeroing the Z axis, Close Keypad to continue."                                                                ' Keypad open
CN,2                                                                 ' Zero Z Axis to Z Zero Plate
$atcUU[].PlateOffset = %(9) - $toolsUU[$ATC.ToolIn][&u].h            ' Record Offset in config
'PAUSE "tool.h- " + $toolsUU[$ATC.ToolIn][&u].h + " ATCPlateOffset- " + $atcUU[&u].PlateOffset
PAUSE "Zeroing to bed surface (or material) now complete.", OKTEXT="Continue ..."
END

' Subroutines ==========================================================
HOME_Z:
    GOSUB Check_Z_Prox
    PZ, $sb_searchDistUU[&u].z, $sb_searchSpeedUU[&u].z, $sb_proxNum.z    ' PZ "probes" for a switch or contact in the Z axis
    ZZ                                                               ' Reset working zero to support next move
    JZ, (-1 * $sb_homeOffUU[&u].z)                                   ' Pull back from prox to location that will be true zero
    &target_ck = $sb_homeOffUU[&u].z + $sb_oochUU[&u]                ' New target for detecting error; just past prox
    PZ, &target_ck, $sb_slowSearchSpeedUU[&u].z, $sb_proxNum.z       ' Slowly probe for prox in final accurate check
    IF %(3) == &target_ck THEN GOSUB fail_missed_target              ' Look at current Z location to see if we went past the target
    ZZ                                                               ' If OK, reset working zero again to do the pullback
    JZ, (-1 * $sb_homeOffUU[&u].z)

   ' VA,,,-1*($toolsUU[$ATC.ToolIn][&u].h + $atcUU[&u].PlateOffset)        ' Apply Offset for no tool
    VA,,,-1*($toolsUU[$ATC.ToolIn][&u].H + $atcUU[&u].PlateOffset),,,,,,0    ' i.e. cutter-to-plate-offset + plate-to-table-offset

    RETURN

' Error Messages =======================================================
fail_missed_target:
    PAUSE "Target Not Triggered! Lower Z Axis and run C3 (XYZ Homing)."  ' Did not hit target.
    END

Check_Z_Prox:
    IF %($sb_proxCk.z) == 0 THEN RETURN
    PAUSE "A Proximity Sensor is Unexpectedly Triggered! Please clear switches and try macro again.", OKTEXT="OK"
    END
    RETURN      
