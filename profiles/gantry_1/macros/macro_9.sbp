'!FABMO!name:Tool Change
'!FABMO!description:Use ATC or manual tool change
'Macro 9
JA,2.5 'This moves the A axis up to a safe height for tool changes. 
'SC,0
SO,1,0
Pause, 2
&ToolCount = 5
&PlateOffset = 0
&WorkZoffset = 0
&SafeClip= 4.625
&XYMoveSpeed = %(71)
&XYJogSpeed = %(76)
&ZMoveSpeed = %(73)
&ZJogSpeed = 6
MS,6,6
JS,15,15

&offZ = &PlateOffset
&cnt = 0

&MyToolin = $Toolin

&clipX = 0
&clipY = 0
&clipZ = 0
'&ToolL = 0

&clip1X = 6.230
&clip1Y = 54.95
&clip1Z = -3.775
'&Tool1L = 11.11

&clip2X = 12.306
&clip2Y = 54.95
&clip2Z = -3.775
'&Tool2L = 10.75

&clip3X = 18.50
&clip3Y = 54.95
&clip3Z = -3.775
'&Tool3L = 4.619

&clip4X = 24.856
&clip4Y = 54.95
&clip4Z = -3.775
'&Tool4L = 10.75

&clip5X = 31.086
&clip5Y = 54.95
&clip5Z = -3.775
'&Tool5L = 10.75

if %(57) = 0 then gosub SetZeroTool
if &tool = &MyToolin then goto ExitAllGood
if &tool > &ToolCount THEN GOTO TOOL_NUM_ERROR
if &MyToolin > &ToolCount THEN GOTO TOOL_NUM_ERROR
if &MyToolin = 0 then GOTO SetClip0in
if &MyToolin = 1 then GOTO SetClip1in
if &MyToolin = 2 then GOTO SetClip2in
if &MyToolin = 3 then GOTO SetClip3in
if &MyToolin = 4 then GOTO SetClip4in
if &MyToolin = 5 then GOTO SetClip5in
GOTO TOOL_NUM_ERROR

CONTINUE_PICKUP:
MZ,(&ToolL + $PlateOffset)-0.25
VA,,,-0.25,,,,,,-0.25,,,
if &MyToolin > 0 then gosub Drop
if &tool = 0 then  GOTO SetClip0
if &tool = 1 then  GOTO SetClip1
if &tool = 2 then  GOTO SetClip2
if &tool = 3 then  GOTO SetClip3
if &tool = 4 then  GOTO SetClip4
if &tool = 5 then  GOTO SetClip5
GOTO TOOL_NUM_ERROR
GOOD_FOR_PICKUP:
$Toolin = &tool
Gosub pickup

ExitAllGood:
MS,&XYMoveSpeed,&ZMoveSpeed
JS,30,20
END

'Subs


Drop:
	J2,&clipX,&clipY - &SafeClip
	'MX,&clipX + 1.5
	JZ,&clipZ
    M2,&clipX,&clipY - 0.25
	SO,8,1    
	M2,&clipX,&clipY 
   ' Pause,.5
    &cnt = 0
    gosub Check8
	JZ,&clipZ + 3.25
	SO,8,0
	$Toolin = 0
Return
GOTO ZEROTOOLERROR

PickUp:
	J2,&clipX,&clipY 
	SO,8,1
    JZ,&clipZ + 3.25
    &cnt = 0
    gosub Check8
	MZ,&clipZ
	SO,8,0
    &cnt = 0
    gosub Check7
	MY,&clipY - &SafeClip
	JZ,0
    VA,,,&ToolL + $PlateOffset,,,,,,&ToolL + $PlateOffset
	MZ,&ToolL + $PlateOffset-0.25
	PAUSE 0.1
Return
GOTO ZEROTOOLERROR

SetClip0:
	&clipX = 0
	&clipY = 0
	&clipZ = 0
    &ToolL = 0
    JZ,0
    VA,,,&ToolL + $PlateOffset,,,,,,&ToolL + $PlateOffset
	MZ,&ToolL + $PlateOffset-0.25
IF &tool = 0 THEN GOTO ExitAllGood
GOTO TOOL_NUM_ERROR

SetClip1:
	IF $Tool1Present = 0 THEN GOTO TOOLERROR
	&clipX = &clip1X
	&clipY = &clip1Y
	&clipZ = &clip1Z
    &ToolL = $Tool1L
GOTO GOOD_FOR_PICKUP

SetClip2:
	IF $Tool2Present = 0 THEN GOTO TOOLERROR
	&clipX = &clip2X
	&clipY = &clip2Y
	&clipZ = &clip2Z
    &ToolL = $Tool2L
GOTO GOOD_FOR_PICKUP

SetClip3:
    IF $Tool3Present = 0 THEN GOTO TOOLERROR
	&clipX = &clip3X
	&clipY = &clip3Y
	&clipZ = &clip3Z
    &ToolL = $Tool3L
GOTO GOOD_FOR_PICKUP

SetClip4:
	IF $Tool4Present = 0 THEN GOTO TOOLERROR
	&clipX = &clip4X
	&clipY = &clip4Y
	&clipZ = &clip4Z
    &ToolL = $Tool4L
GOTO GOOD_FOR_PICKUP

SetClip5:
	IF $Tool5Present = 0 THEN GOTO TOOLERROR
	&clipX = &clip5X
	&clipY = &clip5Y
	&clipZ = &clip5Z
    &ToolL = $Tool5L
GOTO GOOD_FOR_PICKUP

SetClip0in:
	&clipX = 0
	&clipY = 0
	&clipZ = 0
    &ToolL = 0
    &WorkZoffset = %(8)
    IF %(57) = 1 THEN GOTO ZEROTOOLERROR
GOTO CONTINUE_PICKUP

SetClip1in:
	&clipX = &clip1X
	&clipY = &clip1Y
	&clipZ = &clip1Z
    &ToolL = $Tool1L
GOTO CONTINUE_PICKUP

SetClip2in:
	&clipX = &clip2X
	&clipY = &clip2Y
	&clipZ = &clip2Z
	&ToolL = $Tool2L 
GOTO CONTINUE_PICKUP

SetClip3in:
	&clipX = &clip3X
	&clipY = &clip3Y
	&clipZ = &clip3Z
	&ToolL = $Tool3L
GOTO CONTINUE_PICKUP

SetClip4in:
	&clipX = &clip4X
	&clipY = &clip4Y
	&clipZ = &clip4Z
	&ToolL = $Tool4L
GOTO CONTINUE_PICKUP
    
SetClip5in:
	&clipX = &clip5X
	&clipY = &clip5Y
	&clipZ = &clip5Z
	&ToolL = $Tool5L
GOTO CONTINUE_PICKUP
    
TOOLERROR:
PAUSE "No tool in clip number " + &tool
GOSUB SetZeroTool
END
    
SetZeroTool:  
    &MyToolin = 0
    $Toolin = 0
return

Check7:
	&cnt = &cnt + 1
    if &cnt = 75 then goto Fail7
    PAUSE,0.05
	if %(57) = 0 then goto Check7
return

Check8:
Recheck8:
	&cnt = &cnt + 1
    if &cnt = 75 then goto Fail8
    PAUSE,0.05
	if %(58) = 0 then goto Recheck8
return

Fail8:
    SO,8,0
	pause "DrawBar did not open"
END

Fail7:
	pause "Tool not picked up"
END

ZEROTOOLERROR:
PAUSE "Please remove tool from spindle and rehome machine."
END

TOOL_NUM_ERROR:
PAUSE "Invalid tool number " + &tool
END