'!FABMO!name:Home Tool
'!FABMO!description:Automatically Set Axis Zero for XYZ
' -------- Settings Section --------
' Values in this section may be changed to suit different homing configurations
&homing_input = 3
&homing_speed = 3.0
&x_search = -38.0
&x_search_speed = 3.0
&x_backoff = 0.5

&y_search = -26.0
&y_backoff = 0.5
&y_search_speed = 3.0

&z_search = 8.0
&z_backoff = -0.5
&z_search_speed = 1.5

&home_position_x = 1.25
&home_position_y = 1.25

' -------- Main Program Section --------
GOSUB backoff_if_needed
GOSUB home_x
GOSUB home_y
GOSUB home_z
GOSUB go_home

END

' -------- Subroutines --------
' The functions in this section are called by the main program
home_x:
	GOSUB get_positions
    PX,&xpos+&x_search,&x_search_speed,&homing_input
    ZX
    JX,&x_backoff
    ZX
    RETURN

home_y:
	GOSUB get_positions
    PY,&ypos+&y_search,&y_search_speed,&homing_input
    ZY
    JY,&y_backoff
    ZY
    RETURN

home_z:
	GOSUB get_positions
    PZ,&zpos+&z_search,&z_search_speed,&homing_input
    ZZ
    JZ,&z_backoff
    ZZ
    RETURN

go_home:
	J2 &home_position_x, &home_position_y
    Z2
    
get_positions:
	&xpos = %(1)
    &ypos = %(2)
    &zpos = %(3)
    &homing_input_value = %(51)
    RETURN

backoff_if_needed:
	GOSUB get_positions
    IF &homing_input_value == 1 THEN GOSUB backoff
    RETURN
    
backoff:
	GOSUB get_positions
    M3,&xpos+&x_backoff,&ypos+&y_backoff,&zpos+&z_backoff
    RETURN