'!FABMO!name: Park
'!FABMO!description: Move Tool to a Parking Location 
'!FABMO!enabled:true

' Name: Park
' Description: Move Tool to a Parking Location (typically to back)
' Platform: Desktop
' Author: Ted Hall, Ryan Sturmer
' History:
'  2/2/17, 3/16/17 th
' 2021/10/15 refactored (th)

'--- Adjust Standard Macro Variables to Current Tool Units if Needed ---
'--- Check to make sure Standard Macro Variables have been read
$sb_standard_variables_PRESENT := 0								  ' Define test variable if it does not exist
IF $sb_standard_variables_PRESENT == 0 THEN GOSUB no_variables

' === Main Program ===
  GOSUB initialize_variables
  GOSUB Move_to_Position
  END
'====================

'--- 'Subroutines ---
Move_to_Position:
  IF &Z_park < %(3) THEN GOTO Skip_Lift
  	JZ, &Z_park
  Skip_Lift:
    J2, &X_park, &Y_park
  END

no_variables:
    C201
    RETURN

'--- Initialize Variables and Adjust Standard Macro Variables for Current Tool and Conditions as Needed
initialize_variables:
  'Adjust to current UNITS of tool (handles whether tool Macro Variables are for INCHES or MM)
	&dist_mult = 1.0
	IF %(25) == 1 THEN GOTO tool_MM					 			' Read UNIT of Tool; System Variable #25
	  tool_IN:
	    IF $sb_varTableUNITS == "IN" THEN GOTO continue_with_variables
		  &dist_mult = 0.0393701                                ' Defined in MM: Multiplier to redefine distances from mm to inches 
	    GOTO continue_with_variables
	  tool_MM:
	    IF $sb_varTableUNITS == "MM" THEN GOTO continue_with_variables
	      &dist_mult = 25.4                                     ' Defined in IN: Multiplier to redefine distances from inches to mm 
	    GOTO continue_with_variables
  'continue adjusting units for current working variables ...
	continue_with_variables:
		&X_park = $sb_X_park * &dist_mult
		&Y_park = $sb_Y_park * &dist_mult
		&Z_park = $sb_Z_park * &dist_mult
	RETURN

