'!FABMO!name:Reset Cutter Length
'!FABMO!description:... defines offset for Z-Zero location for #3, and sets Axis Zero for Z (use after changing cutter
'Reset Cutter Length (sometimes called Z-Zero) -- MACRO #2 -- [C2] in OpenSBP Language // HANDIBOT VERSION
' ... written in OpenSBP; 3/1/2016 th

'==> Determines the cutter length offset for automated setting of Z-Axis Zero in Macro #3
'    And, it is simultaneously setting the new Axis Zero for Z

'Initialize

    'Do an XYZ Zero so all axes are in a known location so we go to the right target location for zeroing
        C3   ' calls Macro#3                        

    'Then, Move to Zeoring Target Location
        M2, 6, 0
    
    '(... next lines displays; feature of OpenSBP)
    'Attach Aligator Clip to Cutter Shank and Swing-Out Target (-Continue- when Ready)
        PAUSE

    'Make sure we're not accidentally in electrical contact with target 
'        IF %(51) = 1 THEN GOTO Already_Triggered   ' *not working yet ...

    'Set Zero Plate Adder (i.e. thickness) if not present
        $zero_plate_adder := .75
        
    'Remove the Cutter Offset Contribution to Location    
        VA,,, -.25,,,,,, -.25

    'Get Speeds at Startup
		&startXY_speed = %(71)
		&startZ_speed = %(73)
		&startZ_ramp = %(83)

    'Starting Settings for Measuring Z
        VS, 4, .5        
        &Z_plunge_target = -5
        &Z_pullup = .5
        &Z_final = $zero_plate_adder + .25

'HERE WE GO ! .............................................................
    'Next command creates an interrupt handler for contact with zero plate
        VS,,0.5
        VR,, 10000  ' quick stop
        ON INPUT(1,1) GOTO afterHit
        MZ, &Z_plunge_target
            GOTO FAIL
        afterHit:
            GOSUB Backoff

    'Now, let's do again with a touch at a reliable distance and speed
        VS,,.25
        VR,, 10000  ' quick stop
        ON INPUT(1,1) GOTO Finish
        MZ, &Z_plunge_target
            GOTO FAIL
        Finish:
            GOSUB Backoff

    'Set Location and Zero
        $current_cutter_Zoffset = (-1 * %(3)) + $zero_plate_adder + &Z_pullup  ' remember we're pulled up here
        &new_base_Z_loc = &Z_pullup + $zero_plate_adder
        VA,,, &new_base_Z_loc,,,,,, &new_base_Z_loc

    'Restore Values
        VS, &startXY_speed, &startZ_speed
        VR, , &startZ_ramp 
        MZ, &Z_final        

    'Measurement Complete. REMOVE Aligator Clip and PARK Target !! (-Continue- when Done)
        PAUSE

    '{{Eventually we'd like a choice here to move home or to park}}
        
END

'SUBROUTINES BELOW HERE ---------------------------

Backoff:
    &NextPullUp = %(3)
    &NextPullUp = &NextPullUp + &Z_pullup
    MZ, &NextPullUp
    RETURN

FAIL:
    VS, &startXY_speed, &startZ_speed
    VR, , &startZ_ramp 
    'No Contact with Plate ... Ending!
    PAUSE
    END

Already_Triggered:
    'Z-contact seems already triggered ... Ending!
    PAUSE
    END
   
