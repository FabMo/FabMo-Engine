'!FABMO!name:Set Zero Locations
'!FABMO!description:Automatically Set Axis Zero for XYZ
'Set XYZ Zero Locations -- MACRO #3 -- [C3] in OpenSBP Language // HANDIBOT VERSION
' ... written in OpenSBP; 3/1/2016 th

'Initialize
    'Read Offset or Set Defaults
        $current_cutter_Zoffset := .25
        $x_backoff := .10
        $y_backoff := .10
    'Get Speeds at Startup
		&startXY_speed = %(71)
		&startZ_speed = %(73)
	'Get Acc/Dec Ramps at Startup
		&startXY_ramp = %(81)  ' X and Y ramps are the same, treated as single parameter in OpenSBP
		&startZ_ramp = %(83)
    VS, 4,4				' Set Approach Speeds
    VR, 250, 250    	' Set Aggressive Ramp to reduce pounding ...
	ZT,					' ??Initialize all locations and TBC's to zero
						' [* To insure consistency, tool needs to be at nomimal "Full Step" position.
                        '     So this macro is best run just after powering-on your Handibot.]

'[Z Axis is set first to make sure cutter is clear before making X or Y move.]
'Z-Axis Set Zero Routine (setting the Table Base Zero Coordinate; BASED ON CUTTER LENGTH OFFSET)
	MZ, 5.0				' Hit Top Hardstop
	PAUSE .25
    MZ, 4.5				' Position for final approach
    VS, ,5				' ... approach fast (less force)
    VR, ,1000           ' ... limit ramp bounce
    MZ, 5.5
	&z_dist = 0.0 + $current_cutter_Zoffset
    VA, ,, &z_dist,,,,,, &z_dist          ' Set Z Table Base Coordinate (offset to Zero) 
    VS, ,4				' ... reset
	VR, ,50
	&tempPark = $current_cutter_Zoffset - .25
	MZ, &tempPark		' Move to X-Zero // Z-Zero location now accurate

'X-Axis Set Zero Routine (setting the Table Base Zero Coordinate)
	MX, 7.0				' Hit Right-Side Hardstop
	PAUSE .25
    MX, 6.0				' Position for final approach
    VS, 5				' ... approach fast (less force)
    VR, 1000            ' ... limit ramp bounce
    MX, 8.0
	PAUSE .25
	&temp_loc = 6 + $x_backoff
    VA, &temp_loc,,,,,, &temp_loc ' Set X Table Base Coordinate (offset to Zero) 
    VS, 4				' ... reset
	VR, 50			
	MX, 0.0				' Move to X-Zero // X-Zero location now accurate

'Y-Axis Set Zero Routine (setting the Table Base Zero Coordinate)
	MY, 9.0				' Hit Rear Hardstop
	PAUSE .25
    MY, 8.0				' Position for final approach
    VS, 5				' ... approach fast (less force)
    VR, 1000            ' ... limit ramp bounce
    MY, 10.0
	PAUSE .25
	&temp_loc = 8 + $y_backoff
    VA, ,&temp_loc ,,,,,, &temp_loc' Set Y Table Base Coordinate (offset to Zero) 
    VS, 4				' ... reset
	VR, 50			
	MY, 0.0				' Move to Y-Zero // Y-Zero location now accurate


' Reset Starting Values
    VS, &startXY_speed, &startZ_speed
    VR, &startXY_ramp, &startZ_ramp

END